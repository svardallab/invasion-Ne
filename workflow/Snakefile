# Workaround CALCUA VSC requirements about conda environments and containers
COMMON = "calcua.sh"
MODELS = ["constant_demography"]
PROGRAMS = ["gone", "ibdne"]
NUM_CHR = 24
REPLICATES = 10
SAMPLE_SIZES = [20, 200]


include: "rules/simulations.smk"
include: "rules/gone2.smk"
include: "rules/ibdne.smk"
include: "rules/hapne.smk"


rule all:
    input:
        expand(
            "results/estimates/{method}_{model}.csv",
            model=MODELS, method = ["gone2", "ibdne"]
        )


rule aggregate_gone2:
    input:
        "workflow/scripts/aggregate_gone.R",
        expand(
            "steps/gone2/{{model}}/s{seed}_n{n}_GONE2_Ne",
            seed=[100 + i for i in range(REPLICATES)],
            n=SAMPLE_SIZES,
        ),
    output:
        "results/estimates/gone2_{model}.csv",
    shell:
        """
        source {COMMON}
        Rscript {input} {output}
        """

rule aggregate_ibdne:
    input:
        "workflow/scripts/aggregate_ibdne.R",
        expand(
            "steps/ibdne/{{model}}/s{seed}_n{n}.ne",
            seed=[100 + i for i in range(REPLICATES)],
            n=SAMPLE_SIZES,
        ),
    output:
        "results/estimates/ibdne_{model}.csv",
    shell:
        """
        source {COMMON}
        Rscript {input} {output}
        """
